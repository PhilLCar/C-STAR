<forward: loop-block>
<forward: statement>
<forward: type>
<forward: op-func>

// PRIORITY 19 RL
<func-assoc> ::= <sequence> [ <contiguous> <func-assoc> ]

// PRIORITY 18 LR
<sequence> ::= [ <sequence> "," ] <assignment>

// PRIORITY 17 RL
<assignment> ::= <pairing> [ ( "="    |
                               "+="   |
                               "-="   |
                               "*="   |
                               "/="   |
                               "%="   |
                               "+^="  |
                               "-^="  |
                               "*^="  |
                               "/^="  |
                               "<<<=" |
                               "<<="  |
                               ">>="  |
                               ">>>=" |
                               "|>="  |
                               "<|="  |
                               "&="   |
                               "^="   |
                               "|="   |
                               "~=" ) <assignment> ]

// PRIORITY 16 RL
<pairing> ::= <ternary> [ "::" <pairing> ]

// PRIORITY 15 RL
<ternary> ::= <log-or> [ "?" <ternary> ":" <ternary>       |
                         [ "=>" <ternary> ] ".." <ternary> |
                         <raw[0]: ternary-operator> <ternary> <raw[1]: ternary-operator> <ternary> ]

// PRIORITY 14 LR
<log-or>  ::= [ <log-or> "||" ] <log-xor>

// PRIORITY 13 LR
<log-xor> ::= [ <log-xor> "^^" ] <log-and>

// PRIORITY 12 LR
<log-and> ::= [ <log-and> "&&" ] <bit-or>

// PRIORITY 11 LR
<bit-or>  ::= [ <bit-or> "|" ] <bit-xor>

// PRIORITY 10 LR
<bit-xor> ::= [ <bit-xor> "^" ] <bit-and>

// PRIORITY 9 LR
<bit-and> ::= [ <bit-and> "&" ] <comparative>

// PRIORITY 8 LR
<comparative> ::= [ <comparative> ( "==" | "===" | "!=" | "!==" ) ] <relational>

// PRIORITY 7 LR
<relational> ::= [ <relational> ( "<" | ">" | "<=" | ">=" ) ] <shift>

// PRIORITY 6 LR
<shift> ::= [ <shift> ( "<<<" | "<<" | ">>" | ">>>" | "|>" | "<|" ) ] <additive>

// PRIORITY 5 LR
<additive> ::= [ <additive> ( "+" | "-" | <raw: binary-operator> ) ] <multiplicative>

// PRIORITY 4 LR
<multiplicative> ::= [ <multiplicative> ( "*" | "/" | "%" ) ] <power>

// PRIORITY 3 LR
<power> ::= [ <power> ( "*^" | "/^" ) ] <p2>

// PRIORITY 2 RL
<p2> ::= <prefix> | <stack> | <op-new> | <op-redim> | <op-delete> | <type-op> | <unary> | <not> | <cast> | <deref> | <ref> | <p1>
<prefix>    ::= ( "++" | "--" | "+^" | "-^" | "<:" ) <p2>
<stack>     ::= "push" <p2>
<op-new>    ::= "new" [ "(" <sequence> ")" | "[" <sequence> "]" ] <type> [ "{" [ <sequence> ] "}" ]
<op-redim>  ::= "redim" ( "(" <sequence> ")" | "[" <sequence> "]" ) <p2> [ "{" [ <sequence> ] "}" ]
<op-delete> ::= "delete" [ "(" ")" | "[" "]" ] <p2>
<type-op>   ::= ( "sizeof" | "typeof" ) ( "(" <type> | <func-assoc> ")" | <type> | <p2> )
<unary>     ::= ( "+" | "-" | <raw: unary-operator> ) <p2>
<not>       ::= ( "!" | "~" ) <p2>
<cast>      ::= "(" <type> ")" <p2>
<deref>     ::= "*" <p2>
<ref>       ::= "&" <p2>

// PRIORITY 1 LR
<p1> ::= <postfix> | <call> | <index> | <specialisation> | <access> | <p-access> | <kv-access> | <structure> | <value>
<postfix>        ::= <p1> ( "++" | "--" | "!*" | "!/" | "!" | ":>" )
<call>           ::= <p1> "(" [ <sequence> ] ")" ! "{"
<index>          ::= <p1> "[" <sequence> "]"
<specialisation> ::= <p1> "<" <type> { "," <type> } ">"
<access>         ::= <p1> "."  <raw: variable>
<p-access>       ::= <p1> "->" <raw: variable>
<kv-access>      ::= <p1> "[" [ <sequence> ] ":" [ <sequence> ] "]"

// STRUCTURES
<structure> ::= <list> | <array> | <lambda> | <parent>
<list>      ::= "[" [ <sequence> | <loop-block> ] "]"
<array>     ::= "{" [ <sequence> | <loop-block> ] "}"
<lambda>    ::= "(" [ <parameter> { "," <parameter> } ] ")" "{" { <statement> } "}"
<parameter> ::= [ <type> ] <raw: variable> [ "=" <sequence> ]
<parent>    ::= "(" <func-assoc> ")"

// BASE VALUE
<value> ::= <raw: variable> | <raw: string> | <raw: integer> | <raw: decimal> | <raw: constant> | <op-func>