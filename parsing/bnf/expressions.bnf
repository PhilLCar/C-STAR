<forward: statement>
<forward: type>

// PRIORITY 19 RL
<expression> ::= <sequence> <expression> | <sequence>

// PRIORITY 18 LR
<sequence> ::= <sequence> "," <assignment> | <assignment>

// PRIORITY 17 RL
<assignment> ::= <pairing> ( "="    | 
                             "+="   |
                             "-="   |
                             "*="   |
                             "/="   |
                             "%="   |
                             "+^="  |
                             "-^="  |
                             "*^="  |
                             "/^="  |
                             "<<<=" |
                             "<<="  |
                             ">>="  |
                             ">>>=" |
                             "|>="  |
                             "<|="  |
                             "&="   |
                             "^="   |
                             "|="   |
                             "~=") <assignment> | <pairing>

// PRIORITY 16 RL
<pairing> ::= <ternary> "::" <pairing> | <ternary>

// PRIORITY 15 RL
<ternary> ::= <ternary> "?" <log-or> ":" <log-or> | <log-or>

// PRIORITY 14 LR
<log-or>  ::= <log-or> "||" <log-xor> | <log-xor>

// PRIORITY 13 LR
<log-xor> ::= <log-xor> "^^" <log-and> | <log-and>

// PRIORITY 12 LR
<log-and> ::= <log-and> "&&" <bit-or> | <bit-or>

// PRIORITY 11 LR
<bit-or>  ::= <bit-or> "|" <bit-xor> | <bit-xor>

// PRIORITY 10 LR
<bit-xor> ::= <bit-xor> "^" <bit-and> | <bit-and>

// PRIORITY 9 LR
<bit-and> ::= <bit-and> "&" <comparative> | <comparative>

// PRIORITY 8 LR
<comparative> ::= <comparative> ( "==" | "===" | "!=" | "!==" ) <relational> | <relational>

// PRIORITY 7 LR
<relational> ::= <relational> ( "<" | ">" | "<=" | ">=" ) <shift> | <shift>

// PRIORITY 6 LR
<shift> ::= <shift> ( "<<<" | "<<" | ">>" | ">>>" | "|>" | "<|" ) <additive> | <additive>

// PRIORITY 5 LR
<additive> ::= <additive> ( "+" | "-" ) <multiplicative> | <multiplicative>

// PRIORITY 4 LR
<multiplicative> ::= <multiplicative> ( "*" | "/" | "%" ) <power> | <power>

// PRIORITY 3 LR
<power> ::= <power> ( "*^" | "/^" ) <p2> | <p2>

// PRIORITY 2 RL
<p2> ::= <prefix> | <unary> | <not> | <cast> | <deref> | <ref> | <p1>
<prefix>  ::= ( "++" | "--" | "+^" | "-^" | "<:") <p2>
<unary>   ::= ( "+" | "-" ) <p2>
<not>     ::= ( "!" | "~" ) <p2>
<cast>    ::= "(" <type> ")" <p2>
<deref>   ::= "*" <p2>
<ref>     ::= "&" <p2>

// PRIORITY 1 LR
<p1> ::= <postfix> | <call> | <index> | <access> | <p-access> | <parent> | <list> | <array> | <lambda> | <value>
<postfix>   ::= <p1> ( "++" | "--" | "!*" | "!/" | "!" | ":>")
<call>      ::= <p1> "(" [ <expression> ] ")"
<index>     ::= <p1> "[" <expression> "]"
<access>    ::= <p1> "."  <raw: variable>
<p-access>  ::= <p1> "->" <raw: variable>
<parent>    ::= "(" <expression> ")"
<list>      ::= "[" [ <expression> ] "]"
<array>     ::= "{" [ <expression> ] "}"
<lambda>    ::= "(" [ <parameter> { "," <parameter> } ] ")" "{" { <statement> } "}"
<parameter> ::= [ <type> ] <raw: variable> [ "=" <expression> ]

// BASE VALUE
<value> ::= <raw: variable> | <raw: string> | <raw: integer> | <raw: decimal> | <raw: constant>