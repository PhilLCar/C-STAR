;; include (parsing/bnf/expressions.bnf)
;; include (parsing/bnf/types.bnf)
#;; include (parsing/bnf/oop.bnf)
#;; include (parsing/bnf/operator.bnf)

<program> ::= { <statement> }

<statement> ::= ";" | <address> <statement> | <special-block> | <block> /*| <class-decl> | <op-assoc>*/ 

<block>       ::= "{" { <statement> } "}" | <line>
<line>        ::= ( <declaration> | <control> | <func-assoc> ) [ ";" ]
<declaration> ::= <type> <raw: variable> ( <prototype> | [ "=" ] <func-assoc> ) /*| <class-type> | <proto-op>*/
<prototype>   ::= "(" [ <type> [ <raw: variable> ] { "," <type> [ <raw: variable> ] } ] ")"
<control>     ::= "goto" <raw: variable> | ("break" | "continue" | "finish" | "return" | "push") [ <func-assoc> ] | "pop"

<special-block> ::= <if-block> | <switch-block> | <loop-block>
<condition>     ::= <sequence> - <lambda>
<do-block>      ::= "do" <statement>
<if-block>      ::= "if" <condition> <statement> [ "else" <statement> ]
<loop-block>    ::= [ <do-block> ] ( <while-block> | <for-block> | <foreach-block> | <metafor-block> )
<while-block>   ::= "while" <condition> <statement>
<for-block>     ::= "for" "(" [ [ <type> ] <func-assoc> ] ";" [ <condition> ] ";" [ <func-assoc> ] ")" <statement>
<foreach-block> ::= "for" "(" [ <type> ] <raw: variable> ( "in" | "of" | "from" ) <condition> ")" <statement>
<switch-block>  ::= "switch" <condition> "{" { ( "case" <condition> | "default" ) ":" { <statement> } } "}"
<metafor-block> ::= "meta" "for" "(" [ <type> ] <raw: variable> "in" <condition> ")" <statement>

<address> ::= <raw: variable> ":"