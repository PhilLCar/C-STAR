;; include (parsing/bnf/expressions.bnf)
#;; include (parsing/bnf/oop.bnf)
#;; include (parsing/bnf/operator.bnf)

<program> ::= { <statement> }

<statement> ::= <block> | <special-blocks> | <address> //| <class-decl> | <operator-assoc>

<block>       ::= <line> | "{" { <statement> } "}" | <func-assoc>
<line>        ::= [ <expression> | <declaration> | <control> ] ( ";" | <raw: newline> )
<declaration> ::= <type> <expression>
<control>     ::= "goto" <raw: variable> | "break" [ <expression> ] | "continue" [ <expression> ] | "finish" [ <expression> ]

<func-assoc>      ::= [ <type> ] <raw: variable> <lambda>
<lambda>          ::= <parameter-block> "{" {<statement> } "}"
<parameter-block> ::= "(" [ <parameter> { "," <parameter> } ] ")"
<parameter>       ::= [ <type> ] <expression>

<special-blocks> ::= <if-block> | <while-block> | <for-block> | <foreach-block> | <switch-block> | <metafor-block>
<do-block>       ::= "do" <block>
<if-block>       ::= "if" <expression> <block> [ "else" <block> ]
<while-block>    ::= [ <do-block> ] "while" <expression> <block>
<for-block>      ::= [ <do-block> ] "for" "(" [ <expression> | <declaration> ] ";" [ <expression> ] ";" [ <expression> ] ")" <block>
<foreach-block>  ::= [ <do-block> ] "for" "each" "(" [ <expression> ] ( "in" | "of" ) <expression> ")" <block>
<switch-block>   ::= "switch" <expression> "{" { <switch-label> ":" { <statement> } } "}"

# var[10, 10, 10] a;
#
# metafor (var x in a) {
# 	// normal access, if you know a's structure
# 	a[x[0], x[1], x[2]] = wtv;
# 	// meta access, if a's structure is unknown
# 	a[meta x] = wtv;
# }
<metafor-block> ::= [ <do-block> ] "meta" "for" "(" [ <declaration> ] "in" <expression> ")" <block>

<address>      ::= <raw: variable> ":"
<switch-label> ::= ( "case" <expression> | "default" ) ":"